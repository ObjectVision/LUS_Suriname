//**********************************************************************************************************************************************************************************************
//**                                                                                                                                                                                          **
//**                                                Configuration written for Suriname Land Use Modelling project, 2012                                                                       **
//**                                                                                                                                                                                          **
//**********************************************************************************************************************************************************************************************
//**                                                                                                                                                                                          **
//**                                           configuration file with configuration of components used to define scenario's                                                                  **
//**                                                                                                                                                                                          **
//**********************************************************************************************************************************************************************************************

container ScenarioComponents: Using = "Units;Geography;Classifications"
{
	container Suitability: using = "Units;Geography;Classifications;Present"
	{
		#include <high_population.dms>
		#include <high_population_new_infrastructure.dms>
	}
	container ClaimSets
	{
		container ParamSets
		{
			container high_population
			{
				parameter<YearRange>      PeriodLength:                       expr = "value(15, YearRange)";

				parameter<nr_inhabitants> Inwoners2010:                       expr = "value(547298.8,nr_inhabitants)", Source = "Bevolkingsprognose: Algemeen Bureau voor de Statistiek (ABS), Suriname";
				parameter<nr_inhabitants> Inwoners2025:                       expr = "value(719067,  nr_inhabitants)", Source = "Bevolkingsprognose: Algemeen Bureau voor de Statistiek (ABS), Suriname";

				parameter<ratio>          ToenameFootPrintPerjaar:            expr = "value(0.04, ratio)",  Source = "Expert Knowledge"; // Geeft aan hoeveel meer hectare per jaar een inwoner gaat gebruiken

				parameter<ratio>          Intensiveringagricultural_large_scale: expr = "value(0.12, ratio)",  Source = "Expert Knowledge"; // Geeft aan hoeveel de landbouw gaat intensiveren in de jaren tot   2020

				parameter<luHa>           Afnameagricultural_scattered:               expr = "value(100000, luHa)", Source = "Expert Knowledge"; // Geeft aan hoeveel hectare zwerf landbouw zal gaan verdwijnen
				parameter<luHa>           Afnameswamp_savanna_forest:           expr = "value(100000, luHa)", Source = "Expert Knowledge"; // Geeft aan hoeveel hectare moeras zal gaan verdwijnen

				parameter<m>              HoutkapPerMeterWeg:                 expr = "value(0,    m)", Source = "Expert Knowledge"; // Geeft aan hoeveel hectare gekapt gaat worden voor de nieuwe weg, niet in dit scenario 
				parameter<luHa>           HoutkapVoorMining:                  expr = "value(0, luHa)", Source = "Expert Knowledge"; // Geeft aan hoeveel hectare gekapt gaat worden voor mining, niet in dit scenario 
			
			}
			container high_population_new_infrastructure: expr = "high_population"
			{
				parameter<m>              HoutkapPerMeterWeg: expr = "value(1500,    m)", Source = "Expert Knowledge"; // Geeft aan hoeveel hectare gekapt gaat worden voor de nieuwe weg 
				parameter<luHa>           HoutkapVoorMining:  expr = "value(2000, luHa)", Source = "Expert Knowledge"; // Geeft aan hoeveel hectare gekapt gaat worden voor de nieuwe weg 
				parameter<nr_inhabitants> Inwoners2025: expr = "high_population/Inwoners2025  + value(23000,  nr_inhabitants)"; //Extra inwoners aangetrokken door nieuwe infrastructurele ontwikkelingen
			}
		}
		container CalcClaimsetTemplate: isTemplate = "True", IsHidden = "True"
		{
			// begin case parameters
			container ParamSet;
			// end caseparameters

			container CaseData
			{
				container Present: expr =
					"for_each_nedv("
						" Classifications/LU/luEndogenous/name"
						",'landuse[value(' + string(id(Classifications/LU/luEndogenous)) +',Classifications/LU/luEndogenous)]'"
						",void"
						",luHa"
					")"
				{
					attribute<luHa> landuse (Classifications/LU/luEndogenous): expr = "float32(pcount(/Present/landuse/endogenous)) * value(2.5 * 2.5, luHa)";
					
					parameter<luHa_per_inh> ha_builtup_per_inhabitant:               expr = "builtup               / ParamSet/Inwoners2010";
					parameter<luHa_per_inh> ha_agricultural_large_scale_per_inhabitant: expr = "agricultural_large_scale / ParamSet/Inwoners2010";
					parameter<luHa_per_inh> ha_agricultural_small_scale_per_inhabitant: expr = "agricultural_small_scale / ParamSet/Inwoners2010";
					parameter<luHa_per_inh> ha_agricultural_wet_per_inhabitant:          expr = "agricultural_wet          / ParamSet/Inwoners2010";
				}
				Container GrowthRates
				{
					parameter<ratio> builtup:               expr = "(float32(1) + ParamSet/ToenameFootPrintPerjaar) ^ float32(ParamSet/PeriodLength)";
					parameter<ratio> agricultural_large_scale: expr = "(float32(1) + ParamSet/Intensiveringagricultural_large_scale) ^ float32(ParamSet/PeriodLength)";
				}
				Container ProposedRoad // Voor het modelleren van de kap van wegen voor de nieuwe infrastructuur
				{
					parameter<m>    length:   expr = "sum(arc_length(/Present/accessibility/preperation/road/proposed/line, m))";
					parameter<luHa> openarea: expr = "(ParamSet/HoutkapPerMeterWeg * length) / value(10000, m2 / luHa)";
					
				}
				parameter<luHa> agricultural_scattered:        expr = " Present/agricultural_scattered - ParamSet/Afnameagricultural_scattered";
				parameter<luHa> builtup:               expr = "(ParamSet/Inwoners2025 * Present/ha_builtup_per_inhabitant)  * GrowthRates/builtup";
				parameter<luHa> agricultural_large_scale: expr = "(ParamSet/Inwoners2025 * Present/ha_agricultural_large_scale_per_inhabitant)  * GrowthRates/agricultural_large_scale";
				parameter<luHa> agricultural_small_scale: expr = " ParamSet/Inwoners2025 * Present/ha_agricultural_small_scale_per_inhabitant";
				parameter<luHa> agricultural_wet:          expr = " ParamSet/Inwoners2025 * Present/ha_agricultural_wet_per_inhabitant";
				parameter<luHa> forest:                   expr = 
					"sum(Present/LandUse) - (agricultural_scattered + builtup + agricultural_large_scale + agricultural_small_scale + agricultural_wet + swamp_savanna_forest  + open_area)";
					// De claim voor forest is een restcategorie, wat er over is aan landuse dat niet geclaimd wordt door de andere typen blijft forest
				parameter<luHa> swamp_savanna_forest:    expr = " Present/swamp_savanna_forest - ParamSet/Afnameswamp_savanna_forest";
				parameter<luHa> open_area:           expr = " Present/open_area + ProposedRoad/openarea +  ParamSet/HoutkapVoorMining";
			}
			attribute<luHa> ClaimHa (Classifications/LU/luEndogenous):
				expr = "='union_data(Classifications/LU/luEndogenous,' + AsList('CaseData/' + Classifications/LU/luEndogenous/name,',') +')'";
			attribute<gsz> Claim (Classifications/LU/luEndogenous):
				expr = "claimHa / value(2.5 * 2.5, luHa / gsz) ";
		}

		container high_population:                 expr = "CalcClaimsetTemplate(ParamSets/high_population)";
		container high_population_new_infrastructure: expr = "CalcClaimsetTemplate(ParamSets/high_population_new_infrastructure)";
	}
}
